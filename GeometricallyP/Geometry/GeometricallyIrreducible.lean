/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import GeometricallyP.Geometry.Basic
import GeometricallyP.Algebra.GeometricallyIrreducible
import GeometricallyP.Mathlib.Topology.Irreducible
import GeometricallyP.Mathlib.AlgebraicGeometry.Scheme
import GeometricallyP.Mathlib.AlgebraicGeometry.Morphisms.UniversallyOpen
import Mathlib.AlgebraicGeometry.Fiber

/-!
# Geometrically irreducible schemes over a field

A scheme `X` over a field `k` is geometrically irreducible if any base change `X_K`
for a field extension `K` of `k` is irreducible.
-/

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

/-- A scheme `X` over a field `k` is geometrically irreducible if any base change `X_K`
is irreducible for a field extension `K` of `k`. -/
abbrev GeometricallyIrreducible {k : Type u} [Field k] {X : Scheme.{u}}
    (s : X âŸ¶ Spec (.of k)) : Prop :=
  Geometrically (fun X â†¦ IrreducibleSpace X) s

instance : ObjectProperty.InheritedFromSource
    (fun (X : Scheme) â†¦ IrreducibleSpace X)
    @Surjective := by
  constructor
  intro X Y f hf hX
  exact f.surjective.irreducibleSpace _ f.continuous

instance : ObjectProperty.IsClosedUnderIsomorphisms
      (fun (X : Scheme) â†¦ IrreducibleSpace X) :=
  .of_inheritedFromSource _ @Surjective

namespace GeometricallyIrreducible

variable {k : Type u} [Field k] {X : Scheme.{u}} (s : X âŸ¶ Spec (.of k))

lemma iff_irreducibleSpace_pullback :
    GeometricallyIrreducible s â†”
      âˆ€ (K : Type u) [Field K] [Algebra k K],
        IrreducibleSpace â†‘(pullback s (Spec (.of K) â†˜ _)) :=
  Geometrically.iff_of_isClosedUnderIsomorphisms _

/-- The affine scheme `Spec R` is geometrically irreducible over `k` if and only if
the `k`-algebra `R` is geometrically irreducible. -/
-- Note: this is nontrivial, because the definition of `Algebra.GeometricallyIrreducible` is
-- quite different.
lemma iff_spec (R : Type u) [CommRing R] [Algebra k R] :
    GeometricallyIrreducible (Spec (.of R) â†˜ Spec (.of k)) â†”
      Algebra.GeometricallyIrreducible k R := by
  rw [iff_irreducibleSpace_pullback]
  constructor
  Â· rw [Algebra.geometricallyIrreducible_iff]
    intro h
    let irred : IrreducibleSpace â†¥(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k R)))
        (Spec.map (CommRingCat.ofHom (algebraMap k (AlgebraicClosure k))))) :=
      h (AlgebraicClosure k)
    have irred_switch_tp : IrreducibleSpace â†¥(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k (AlgebraicClosure k))))
        (Spec.map (CommRingCat.ofHom (algebraMap k R)))) := by
        apply IsHomeomorph.irreducibleSpace _ (pullbackSymmetry _
            (Spec.map (CommRingCat.ofHom (algebraMap k R)))).inv.homeomorph.isHomeomorph
    let equiv := AlgebraicGeometry.pullbackSpecIso k (AlgebraicClosure k) R
    let f := equiv.hom
    let hf : IsHomeomorph f := f.homeomorph.isHomeomorph
    exact IsHomeomorph.irreducibleSpace f hf
  Â· intro h K _ _
    have irred_pb : IrreducibleSpace â†¥(Spec (CommRingCat.of (TensorProduct k K R))) :=
        (Algebra.GeometricallyIrreducible.irreducibleSpace k R) K
    let equiv := AlgebraicGeometry.pullbackSpecIso k K R
    let f := equiv.inv
    let hf : IsHomeomorph f := f.homeomorph.isHomeomorph
    have irred_pb' : IrreducibleSpace â†¥(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k K)))
        (Spec (CommRingCat.of R) â†˜ Spec (CommRingCat.of k)))
      := IsHomeomorph.irreducibleSpace f hf
    exact IsHomeomorph.irreducibleSpace _ (pullbackSymmetry _
        (Spec.map (CommRingCat.ofHom (algebraMap k K)))).inv.homeomorph.isHomeomorph

/-- Every nonempty open subscheme of a geometrically irreducible scheme is geometrically
irreducible. -/
@[stacks 038G "Final statement."]
lemma of_isOpenImmersion (U : Scheme.{u}) (i : U âŸ¶ X) [IsOpenImmersion i] [Nonempty U]
    [GeometricallyIrreducible s] :
    GeometricallyIrreducible (i â‰« s) :=
  -- Alireza
  sorry

/-- If `X` is geometrically irreducible over `k` and `U` is an affine open, `Î“(X, U)` is
geometrically irreducible over `k`. -/
@[stacks 038G "(1) => (2)"]
lemma geometricallyIrreducible_of_isAffineOpen [GeometricallyIrreducible s]
    (U : X.Opens) (hU : IsAffineOpen U)
    (hU' : U.carrier.Nonempty) :
    letI : Algebra k Î“(X, U) := algebraOfHomSpec s U
    Algebra.GeometricallyIrreducible k Î“(X, U) := by
      letI : Algebra k Î“(X, U) := algebraOfHomSpec s U
      let : Nonempty (Spec Î“(X, U)) :=
        Nonempty.intro (hU.isoSpec.hom (Classical.choice hU'.to_subtype))
      let irred : GeometricallyIrreducible (hU.fromSpec â‰« s) :=
        of_isOpenImmersion s (Spec Î“(X, U)) hU.fromSpec

      let adjunction : (Spec (.of k)).toSpecÎ“ â‰« Spec.map ((Scheme.Î“SpecIso <| .of k).inv)
        = ğŸ™ (Spec (.of k)) := by simp
      have : hU.fromSpec â‰« s = Spec.map (CommRingCat.ofHom (algebraMap k Î“(X, U))) :=
        calc hU.fromSpec â‰« s =
          hU.fromSpec â‰« s â‰« (Spec (.of k)).toSpecÎ“ â‰« Spec.map ((Scheme.Î“SpecIso <| .of k).inv) :=
            (by rw [adjunction, Category.comp_id])
          _ = Spec.map (X.presheaf.map (homOfLE le_top).op) â‰« Spec.map s.appTop
            â‰« Spec.map ((Scheme.Î“SpecIso <| .of k).inv) := (by
            rw [â† Category.assoc s (Spec (.of k)).toSpecÎ“
              (Spec.map ((Scheme.Î“SpecIso <| .of k).inv)),
              â† Category.assoc hU.fromSpec _ _, Scheme.toSpecÎ“_naturality s,
              â† Category.assoc, AlgebraicGeometry.IsAffineOpen.fromSpec_toSpecÎ“ hU];rfl)
          _ = Spec.map (CommRingCat.ofHom (algebraMap k Î“(X, U))) := (by
            rw [â† Spec.map_comp, â† Spec.map_comp];congr)

      rw [â† iff_spec]
      simp [this] at irred
      exact irred

/-- Irreducibility of a scheme can be checked on an open cover
with pairwise non-empty intersections. -/
lemma irreducible_of_openCover (ğ’° : X.OpenCover) [Nonempty ğ’°.Iâ‚€]
    (hn : âˆ€ i j, Nonempty â†‘(pullback (ğ’°.f i) (ğ’°.f j)))
    (h : âˆ€ i, IrreducibleSpace (ğ’°.X i)) :
    IrreducibleSpace X := by
  have := ğ’°.isOpenCover_opensRange
  have hn' (i j : _) :
      ((ğ’°.f i).opensRange.carrier âˆ© (ğ’°.f j).opensRange.carrier).Nonempty := by
    simp [â† Scheme.Pullback.range_fst_comp, Set.range_nonempty]
  refine IrreducibleSpace.of_openCover this hn' (fun i â†¦ ?_)
  apply (Set.rangeFactorization_surjective (f := (ğ’°.f i))).irreducibleSpace
  exact continuous_rangeFactorization_iff.mpr (ğ’°.f i).continuous

theorem _root_.nonempty_pullback_baseChange_of_surjective
    {X Uâ‚ Uâ‚‚ S S' : Scheme.{u}} (s : X âŸ¶ S)
    (fâ‚ : Uâ‚ âŸ¶ X) (fâ‚‚ : Uâ‚‚ âŸ¶ X) -- [IsOpenImmersion fâ‚] [IsOpenImmersion fâ‚‚]
    (hn : Nonempty â†¥(pullback fâ‚ fâ‚‚))
    (g : S' âŸ¶ S) [hg : Surjective g] :
    Nonempty â†¥(pullback
      (pullback.map (fâ‚ â‰« s) g s g fâ‚ (ğŸ™ S') (ğŸ™ S) (by simp) (by simp))
      (pullback.map (fâ‚‚ â‰« s) g s g fâ‚‚ (ğŸ™ S') (ğŸ™ S) (by simp) (by simp))) :=
  have : Surjective (pullback.fst (pullback.fst fâ‚ fâ‚‚ â‰« fâ‚ â‰« s) g) := inferInstance
  âŸ¨pullback.lift
    (pullback.map _ _ _ _ (pullback.fst fâ‚ fâ‚‚) (ğŸ™ _) (ğŸ™ _) (by simp) (by simp))
    (pullback.map _ _ _ _ (pullback.snd fâ‚ fâ‚‚) (ğŸ™ _) (ğŸ™ _)
      (by simp [â† Category.assoc, pullback.condition]) (by simp))
    (by simp [pullback.map_comp, pullback.condition])
    (this.surj hn.some).chooseâŸ©

/-- If `X` is covered by geometrically irreducible open subschemes with pairwise
non-empty intersections, `X` is geometrically irreducible. -/
@[stacks 038G "(4) => (1)"]
lemma of_openCover (ğ’° : X.OpenCover) [Nonempty ğ’°.Iâ‚€]
    (hn : âˆ€ i j, Nonempty â†‘(pullback (ğ’°.f i) (ğ’°.f j)))
    (h : âˆ€ i, GeometricallyIrreducible (ğ’°.f i â‰« s)) :
    GeometricallyIrreducible s := by
  refine (Geometrically.iff_of_isClosedUnderIsomorphisms s).mpr (fun K _ _ â†¦ ?_)
  let hpo :=
    Scheme.Pullback.openCoverOfLeft ğ’° s (Spec (CommRingCat.of K) â†˜ Spec (CommRingCat.of k))
  have hi (i : hpo.Iâ‚€) : IrreducibleSpace (hpo.X i) := by
    simp only [Scheme.Pullback.openCoverOfLeft_X, hpo]
    apply (Geometrically.iff_of_isClosedUnderIsomorphisms (ğ’°.f i â‰« s)).mp (h i)
  have : Nonempty hpo.Iâ‚€ := by simp only [Scheme.Pullback.openCoverOfLeft_Iâ‚€, hpo]; infer_instance
  refine irreducible_of_openCover hpo (fun i j â†¦ ?_) hi
  -- Remains only to show that the pullback cover `hpo` has pairwise non-empty intersections.
  apply nonempty_pullback_baseChange_of_surjective s (ğ’°.f i) (ğ’°.f j) (hn i j)
    (Spec (CommRingCat.of K) â†˜ Spec (CommRingCat.of k))
    (hg := (surjective_iff _).mpr <| Function.surjective_to_subsingleton _)

/-- Being geometrically irreducible can be checked on finite extensions. -/
lemma of_finite
    (H : âˆ€ (K : Type u) [Field K] [Algebra k K] [Module.Finite k K] [Algebra.IsSeparable k K],
      IrreducibleSpace â†‘(pullback s (Spec (.of K) â†˜ Spec (.of k)))) :
    GeometricallyIrreducible s :=
  sorry

/-- Being geometrically irreducible can be checked on a separably closed field. -/
lemma of_isSepClosed (Î© : Type u) [Field Î©] [Algebra k Î©] [IsSepClosed Î©]
    [IrreducibleSpace â†‘(pullback s (Spec (.of Î©) â†˜ Spec (.of k)))] :
    GeometricallyIrreducible s :=
  -- Yannis
  sorry

/-- `X` is geometrically irreducible over `s` if and only if `X_K` is irreducible
for `K` a separable closure of `k`. -/
theorem iff_irreducibleSpace_separableClosure :
    GeometricallyIrreducible s â†”
      IrreducibleSpace â†‘(pullback s (Spec (.of <| SeparableClosure k) â†˜ Spec (.of k))) :=
  sorry

/-- If `X` is geometrically irreducible over `k` and `Y` is any `k`-scheme, then
`X Ã—[k] Y âŸ¶ Y` induces an order preserving bijection between irreducible components. -/
@[stacks 0364]
def irreducibleComponentsOrderIsoPullback [GeometricallyIrreducible s] {Y : Scheme.{u}}
    (t : Y âŸ¶ Spec (.of k)) :
    irreducibleComponents Y â‰ƒo irreducibleComponents â†‘(pullback s t) :=
  irreducibleComponentsEquivOfIsPreirreducibleFiber _ (pullback.snd s t).continuous
    -- use `AlgebraicGeometry.universallyOpen_Spec_field`
    (by
      have h : UniversallyOpen s := by infer_instance
      rw [AlgebraicGeometry.universallyOpen_iff] at h
      have is_pb : IsPullback (pullback.snd s t) (pullback.fst s t) t s := by
        apply IsPullback.flip
        exact IsPullback.of_hasPullback _ _
      exact h (pullback.fst s t) t (pullback.snd s t) is_pb
    )
    (by
      intro p
      apply IsIrreducible.isPreirreducible
      let pull_s := pullback.snd s t
      have : IrreducibleSpace â†¥(Scheme.Hom.fiber (pullback.snd s t) p) := by
        have h : GeometricallyIrreducible s := by infer_instance
        rw [iff_irreducibleSpace_pullback] at h
        let kp := Y.residueField p
        let map := (Y.fromSpecResidueField p) â‰« t
        obtain âŸ¨Ï†, hÏ†âŸ© := Spec.map_surjective map
        let : Algebra k kp := Ï†.hom.toAlgebra
        have := h kp
        rw [Scheme.Hom.fiber]
        rw [overHom_Spec_def, RingHom.algebraMap_toAlgebra] at this
        dsimp at this
        let e : pullback (pullback.snd s t) (Y.fromSpecResidueField p) â‰… pullback s (Spec.map Ï†) :=
          pullbackLeftPullbackSndIso _ _ _ â‰ªâ‰« pullback.congrHom rfl hÏ†.symm
        let f := e.inv
        apply IsHomeomorph.irreducibleSpace f f.homeomorph.isHomeomorph
      let f := AlgebraicGeometry.Scheme.Hom.fiberHomeo (pullback.snd s t) p
      have irr := IsHomeomorph.irreducibleSpace f f.isHomeomorph
      exact IsIrreducible.of_subtype _
    )
    (by
      have : Surjective (pullback.snd s t) := by
        apply MorphismProperty.pullback_snd
        constructor
        have : IrreducibleSpace X := by
          apply Geometrically.prop_self s
        apply Function.surjective_to_subsingleton
      exact this.surj
    )

end GeometricallyIrreducible

end AlgebraicGeometry
