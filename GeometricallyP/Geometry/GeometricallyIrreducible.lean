/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import GeometricallyP.Geometry.Basic
import GeometricallyP.Algebra.GeometricallyIrreducible
import GeometricallyP.Mathlib.Topology.Irreducible
import GeometricallyP.Mathlib.AlgebraicGeometry.Scheme
import Mathlib.AlgebraicGeometry.Morphisms.UniversallyOpen

/-!
# Geometrically irreducible schemes over a field

A scheme `X` over a field `k` is geometrically irreducible if any base change `X_K`
for a field extension `K` of `k` is irreducible.
-/

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

/-- A scheme `X` over a field `k` is geometrically irreducible if any base change `X_K`
is irreducible for a field extension `K` of `k`. -/
abbrev GeometricallyIrreducible {k : Type u} [Field k] {X : Scheme.{u}}
    (s : X ‚ü∂ Spec (.of k)) : Prop :=
  Geometrically (fun X ‚Ü¶ IrreducibleSpace X) s

instance : ObjectProperty.InheritedFromSource
    (fun (X : Scheme) ‚Ü¶ IrreducibleSpace X)
    @Surjective := by
  constructor
  intro X Y f hf hX
  exact f.surjective.irreducibleSpace _ f.continuous

instance : ObjectProperty.IsClosedUnderIsomorphisms
      (fun (X : Scheme) ‚Ü¶ IrreducibleSpace X) :=
  .of_inheritedFromSource _ @Surjective

namespace GeometricallyIrreducible

variable {k : Type u} [Field k] {X : Scheme.{u}} (s : X ‚ü∂ Spec (.of k))

lemma iff_irreducibleSpace_pullback :
    GeometricallyIrreducible s ‚Üî
      ‚àÄ (K : Type u) [Field K] [Algebra k K],
        IrreducibleSpace ‚Üë(pullback s (Spec (.of K) ‚Üò _)) :=
  Geometrically.iff_of_isClosedUnderIsomorphisms _

/-- The affine scheme `Spec R` is geometrically irreducible over `k` if and only if
the `k`-algebra `R` is geometrically irreducible. -/
-- Note: this is nontrivial, because the definition of `Algebra.GeometricallyIrreducible` is
-- quite different.
lemma iff_spec (R : Type u) [CommRing R] [Algebra k R] :
    GeometricallyIrreducible (Spec (.of R) ‚Üò Spec (.of k)) ‚Üî
      Algebra.GeometricallyIrreducible k R := by
  rw [iff_irreducibleSpace_pullback]
  constructor
  ¬∑ rw [Algebra.geometricallyIrreducible_iff]
    intro h
    let irred : IrreducibleSpace ‚Ü•(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k R)))
        (Spec.map (CommRingCat.ofHom (algebraMap k (AlgebraicClosure k))))) :=
      h (AlgebraicClosure k)
    have irred_switch_tp : IrreducibleSpace ‚Ü•(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k (AlgebraicClosure k))))
        (Spec.map (CommRingCat.ofHom (algebraMap k R)))) := by
        apply IsHomeomorph.irreducibleSpace _ (pullbackSymmetry _
            (Spec.map (CommRingCat.ofHom (algebraMap k R)))).inv.homeomorph.isHomeomorph
    let equiv := AlgebraicGeometry.pullbackSpecIso k (AlgebraicClosure k) R
    let f := equiv.hom
    let hf : IsHomeomorph f := f.homeomorph.isHomeomorph
    exact IsHomeomorph.irreducibleSpace f hf
  ¬∑ intro h K _ _
    have irred_pb : IrreducibleSpace ‚Ü•(Spec (CommRingCat.of (TensorProduct k K R))) :=
        (Algebra.GeometricallyIrreducible.irreducibleSpace k R) K
    let equiv := AlgebraicGeometry.pullbackSpecIso k K R
    let f := equiv.inv
    let hf : IsHomeomorph f := f.homeomorph.isHomeomorph
    have irred_pb' : IrreducibleSpace ‚Ü•(pullback
        (Spec.map (CommRingCat.ofHom (algebraMap k K)))
        (Spec (CommRingCat.of R) ‚Üò Spec (CommRingCat.of k)))
      := IsHomeomorph.irreducibleSpace f hf
    exact IsHomeomorph.irreducibleSpace _ (pullbackSymmetry _
        (Spec.map (CommRingCat.ofHom (algebraMap k K)))).inv.homeomorph.isHomeomorph

/-- Every nonempty open subscheme of a geometrically irreducible scheme is geometrically
irreducible. -/
@[stacks 038G "Final statement."]
lemma of_isOpenImmersion (U : Scheme.{u}) (i : U ‚ü∂ X) [IsOpenImmersion i] [Nonempty U]
    [GeometricallyIrreducible s] :
    GeometricallyIrreducible (i ‚â´ s) :=
  -- Alireza
  sorry

/-- If `X` is geometrically irreducible over `k` and `U` is an affine open, `Œì(X, U)` is
geometrically irreducible over `k`. -/
@[stacks 038G "(1) => (2)"]
lemma geometricallyIrreducible_of_isAffineOpen [GeometricallyIrreducible s]
    (U : X.Opens) (hU : IsAffineOpen U) (hU : U.carrier.Nonempty) :
    letI : Algebra k Œì(X, U) := algebraOfHomSpec s U
    Algebra.GeometricallyIrreducible k Œì(X, U) :=
  -- use `of_isOpenImmersion` to reduce to the affine case
  -- Cheni
  sorry

lemma _root_.irreducible_of_openCover {X Œπ : Type*} [TopologicalSpace X] [hŒπ : Nonempty Œπ]
    {U : Œπ ‚Üí TopologicalSpace.Opens X} (hU : TopologicalSpace.IsOpenCover U)
    (hn : ‚àÄ i j, ((U i).carrier ‚à© (U j).carrier).Nonempty)
    (h : ‚àÄ i, IrreducibleSpace ‚Ü•(U i)) :
    IrreducibleSpace X := by
  have h' (i : _) : IsIrreducible (U i).carrier :=
    IsIrreducible.of_subtype _
  let i : Œπ := Classical.choice (Œ± := Œπ) hŒπ
  rcases exists_mem_irreducibleComponents_subset_of_isIrreducible (U i).carrier (h' i)
    with ‚ü®u, hu, hUu‚ü©
  by_cases huniv : u = Set.univ
  ¬∑ rw [huniv] at hu
    exact (irreducibleSpace_def _).mpr hu.1
  ¬∑ have huo : IsOpen u·∂ú :=
      IsClosed.isOpen_compl (self := isClosed_of_mem_irreducibleComponents u hu)
    push_neg at huniv
    rw [u.ne_univ_iff_exists_notMem] at huniv
    choose a ha using huniv
    choose j haj using hU.exists_mem a
    rcases Set.inter_nonempty_iff_exists_left.mp
      ((h' j).2 (U i) u·∂ú (U i).isOpen huo (hn j i) ‚ü®a, ‚ü®haj, ha‚ü©‚ü©).right
      with ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü©
    exfalso; exact hx‚ÇÇ <| hUu hx‚ÇÅ

lemma irreducible_of_openCover (ùí∞ : X.OpenCover) [Nonempty ùí∞.I‚ÇÄ]
    (hn : ‚àÄ i j, Nonempty ‚Üë(pullback (ùí∞.f i) (ùí∞.f j)))
    (h : ‚àÄ i, IrreducibleSpace (ùí∞.X i)) :
    IrreducibleSpace X := by
  -- irreducibility can be checked on an open cover
  have := ùí∞.isOpenCover_opensRange
  have hn' : ‚àÄ i j,
      ((ùí∞.f i).opensRange.carrier ‚à© (ùí∞.f j).opensRange.carrier).Nonempty := sorry
    -- nonempty pullback implies nonempty intersection of subsets
    -- Scheme.Pullback.range_fst_comp
  refine _root_.irreducible_of_openCover this hn' (fun i ‚Ü¶ ?_)
  apply (Set.rangeFactorization_surjective (f := (ùí∞.f i))).irreducibleSpace
  exact continuous_rangeFactorization_iff.mpr (ùí∞.f i).continuous

/-- If `X` is covered by geometrically irreducible open subschemes with pairwise
non-empty intersections, `X` is geometrically irreducible. -/
@[stacks 038G "(4) => (1)"]
lemma of_openCover (ùí∞ : X.OpenCover) [Nonempty ùí∞.I‚ÇÄ]
    (hn : ‚àÄ i j, Nonempty ‚Üë(pullback (ùí∞.f i) (ùí∞.f j)))
    (h : ‚àÄ i, GeometricallyIrreducible (ùí∞.f i ‚â´ s)) :
    GeometricallyIrreducible s := by
  refine (Geometrically.iff_of_isClosedUnderIsomorphisms s).mpr (fun K _ _ ‚Ü¶ ?_)
  let hpo :=
    Scheme.Pullback.openCoverOfLeft ùí∞ s (Spec (CommRingCat.of K) ‚Üò Spec (CommRingCat.of k))
  have hi (i : hpo.I‚ÇÄ) : IrreducibleSpace (hpo.X i) := by
    simp only [Scheme.Pullback.openCoverOfLeft_X, hpo]
    apply (Geometrically.iff_of_isClosedUnderIsomorphisms (ùí∞.f i ‚â´ s)).mp (h i)
  have : Nonempty hpo.I‚ÇÄ := by simp only [Scheme.Pullback.openCoverOfLeft_I‚ÇÄ, hpo]; infer_instance
  refine irreducible_of_openCover hpo (fun i j ‚Ü¶ ?_) hi
  -- pullback cover has pairwise non-empty intersections
  sorry

/-- Being geometrically irreducible can be checked on finite extensions. -/
lemma of_finite
    (H : ‚àÄ (K : Type u) [Field K] [Algebra k K] [Module.Finite k K] [Algebra.IsSeparable k K],
      IrreducibleSpace ‚Üë(pullback s (Spec (.of K) ‚Üò Spec (.of k)))) :
    GeometricallyIrreducible s :=
  sorry

/-- Being geometrically irreducible can be checked on a separably closed field. -/
lemma of_isSepClosed (Œ© : Type u) [Field Œ©] [Algebra k Œ©] [IsSepClosed Œ©]
    [IrreducibleSpace ‚Üë(pullback s (Spec (.of Œ©) ‚Üò Spec (.of k)))] :
    GeometricallyIrreducible s :=
  -- Yannis
  sorry

/-- `X` is geometrically irreducible over `s` if and only if `X_K` is irreducible
for `K` a separable closure of `k`. -/
theorem iff_irreducibleSpace_separableClosure :
    GeometricallyIrreducible s ‚Üî
      IrreducibleSpace ‚Üë(pullback s (Spec (.of <| SeparableClosure k) ‚Üò Spec (.of k))) :=
  sorry

/-- If `X` is geometrically irreducible over `k` and `Y` is any `k`-scheme, then
`X √ó[k] Y ‚ü∂ Y` induces an order preserving bijection between irreducible components. -/
@[stacks 0364]
def irreducibleComponentsOrderIsoPullback [GeometricallyIrreducible s] {Y : Scheme.{u}}
    (t : Y ‚ü∂ Spec (.of k)) :
    irreducibleComponents Y ‚âÉo irreducibleComponents ‚Üë(pullback s t) :=
  irreducibleComponentsEquivOfIsPreirreducibleFiber _ (pullback.snd s t).continuous
    -- use `AlgebraicGeometry.universallyOpen_Spec_field`
    sorry
    sorry
    sorry

end GeometricallyIrreducible

end AlgebraicGeometry
