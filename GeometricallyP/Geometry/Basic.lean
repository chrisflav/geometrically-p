/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import GeometricallyP.Mathlib.AlgebraicGeometry.Morphisms.UnderlyingMap
import GeometricallyP.Mathlib.CategoryTheory.ObjectProperty.Stability
import Mathlib.AlgebraicGeometry.Properties

/-!
# Geometrically-`P` schemes over a field

In this file we define the basic interface for properties like geometrically reduced,
geometrically irreducible, geometrically connected etc. In this file
we treat an abstract property of schemes `P` and derive the general properties that are
shared by all of these variants.

A scheme `X` over a field `k` is geometrically `P` if `P` holds for any base change `X_K`
for a field extension `K` of `k`.
-/

universe u

open CategoryTheory Limits

namespace AlgebraicGeometry

noncomputable instance (R S : Type u) [CommRing R] [CommRing S] [Algebra R S] :
    (Spec (.of S)).Over (Spec (.of R)) where
  hom := Spec.map (CommRingCat.ofHom <| algebraMap R S)

@[simp]
lemma overHom_spec_self (R : Type u) [CommRing R] :
    Spec (.of R) â†˜ Spec (.of R) = ðŸ™ _ := Spec.map_id _

/-- If `X` is a scheme over `S` and `f : T âŸ¶ S` is a morphism, the fibre product
`X Ã—[S] T` is a scheme over `T`.
This matches the order in `CategoryTheory.Over.pullback`, but not the tensor product convention. -/
noncomputable instance {X S T : Scheme.{u}} (f : T âŸ¶ S) [X.Over S] :
    (pullback (X â†˜ S) f).Over T where
  hom := pullback.snd _ _

/-- A scheme `X` over a field `k` is geometrically `P` if `P` holds for any base change `X_K`
for a field extension `K` of `k`. -/
@[mk_iff]
class Geometrically (P : ObjectProperty Scheme.{u}) {k : Type u} [Field k] {X : Scheme.{u}}
    (s : X âŸ¶ Spec (.of k)) : Prop where
  prop_of_isPullback (s) (K : Type u) [Field K] [Algebra k K] (Y : Scheme.{u}) (fst : Y âŸ¶ X)
    (snd : Y âŸ¶ Spec (.of K)) (h : IsPullback fst snd s (Spec (.of K) â†˜ Spec (.of k))) :
    P Y

/-- A scheme `X` over a field `k` is geometrically reduced if any base change `X_K`
is reduced for a field extension `K` of `k`. -/
abbrev GeometricallyIsReduced {k : Type u} [Field k] {X : Scheme.{u}}
    (s : X âŸ¶ Spec (.of k)) : Prop :=
  Geometrically (fun X â†¦ IsReduced X) s

/-- A scheme `X` over a field `k` is geometrically connected if any base change `X_K`
is connected for a field extension `K` of `k`. -/
abbrev GeometricallyConnected {k : Type u} [Field k] {X : Scheme.{u}}
    (s : X âŸ¶ Spec (.of k)) : Prop :=
  Geometrically (fun X â†¦ ConnectedSpace X) s

namespace Geometrically

variable {P : ObjectProperty Scheme.{u}} {k : Type u} [Field k] {X : Scheme.{u}}
  (s : X âŸ¶ Spec (.of k))

lemma prop_self [Geometrically P s] : P X :=
  prop_of_isPullback s k X (ðŸ™ X) s <| by simp [IsPullback.of_id_fst]

lemma prop_pullback [Geometrically P s] (K : Type u) [Field K] [Algebra k K] :
    P (pullback s (Spec (.of K) â†˜ _)) :=
  prop_of_isPullback s K _ _ _ (.of_hasPullback _ _)

lemma prop_pullback' [Geometrically P s] (K : Type u) [Field K] [Algebra k K] :
    P (pullback (Spec (.of K) â†˜ _) s) :=
  prop_of_isPullback s K _ _ _ (.flip <| .of_hasPullback _ _)

lemma iff_of_isClosedUnderIsomorphisms [P.IsClosedUnderIsomorphisms] :
    Geometrically P s â†”
      âˆ€ (K : Type u) [Field K] [Algebra k K], P (pullback s (Spec (.of K) â†˜ _)) := by
  refine âŸ¨fun h K _ _ â†¦ prop_pullback _ _, fun H â†¦ ?_âŸ©
  rw [geometrically_iff]
  intro K _ _ Y fst snd h
  exact P.prop_of_iso h.isoPullback.symm (H _)

lemma iff_of_iso [P.IsClosedUnderIsomorphisms] {Y : Scheme.{u}} (t : Y âŸ¶ Spec (.of k)) (e : X â‰… Y)
    (w : e.hom â‰« t = s := by cat_disch) : Geometrically P s â†” Geometrically P t := by
  rw [iff_of_isClosedUnderIsomorphisms, iff_of_isClosedUnderIsomorphisms]
  congr! 3
  apply P.prop_iff_of_iso
  exact (Over.pullback _ â‹™ Over.forget _).mapIso (Over.isoMk e w : Over.mk s â‰… Over.mk t)

/-- If `X âŸ¶ Spec k` is geometrically `P` and `k'` is a field extension of `k`,
then also `X_k' âŸ¶ Spec k'` is geometrically `P`. -/
lemma of_isPullback [P.IsClosedUnderIsomorphisms] {k' : Type u} [Field k']
    [Algebra k k'] {Y : Scheme.{u}} {fst : Y âŸ¶ X} {snd : Y âŸ¶ Spec (.of k')}
    (h : IsPullback fst snd s (Spec (.of k') â†˜ Spec (.of k))) [Geometrically P s] :
    Geometrically P snd :=
  sorry

/--
Suppose the property `P` is preserved by surjective morphisms (i.e., if `X âŸ¶ Y` is surjective
and `X` satisfies `P`, also `Y` satisfies `P`).
Then `Geometrically P` is invariant under scalar extensions.
-/
-- Note: this is in particular satisfied for `P = IrreducibleSpace` and `P = ConnectedSpace`.
@[stacks 054P]
lemma iff_of_inheritedFromSource_surjective_of_isPullback [P.InheritedFromSource @Surjective]
    {k' : Type u} [Field k'] [Algebra k k'] {Y : Scheme.{u}} {fst : Y âŸ¶ X} {snd : Y âŸ¶ Spec (.of k')}
    (h : IsPullback fst snd s (Spec (.of k') â†˜ Spec (.of k))) :
    Geometrically P snd â†” Geometrically P s :=
  have : P.IsClosedUnderIsomorphisms := .of_inheritedFromSource _ @Surjective
  sorry

end Geometrically

end AlgebraicGeometry
