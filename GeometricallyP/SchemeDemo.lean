/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Morphisms.Etale
import Mathlib.AlgebraicGeometry.Morphisms.Proper
import Mathlib.AlgebraicGeometry.Morphisms.ClosedImmersion
import Mathlib.AlgebraicGeometry.Morphisms.UniversallyInjective
import Mathlib.AlgebraicGeometry.ResidueField
import Mathlib.AlgebraicGeometry.FunctionField
import Mathlib.AlgebraicGeometry.Noetherian
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Basic
import Mathlib.RingTheory.RingHom.Flat
import Mathlib.Tactic.DepRewrite

/-!
# Schemes in mathlib

This file contains an introduction to scheme theory in mathlib.
-/

universe u

-- Most declarations are in the `AlgebraicGeometry` namespace
open AlgebraicGeometry CategoryTheory Limits

section PrimeSpectrum

/-! ## Prime spectrum of a ring -/

variable {R S : Type*} [CommRing R] [CommRing S]

-- The `PrimeSpectrum` of a ring is a type.
#check PrimeSpectrum R

-- To provide a term of type `PrimeSpectrum R`, we need to give an ideal of `R`
-- with a proof that `p` is a prime ideal.
example (p : Ideal R) [p.IsPrime] : PrimeSpectrum R := ‚ü®p, (inferInstance : p.IsPrime)‚ü©

-- It is endowed with a structure of topological space.
example : TopologicalSpace (PrimeSpectrum R) :=
  inferInstance

-- `PrimeSpectrum` is functorial wrt. to ring homomorphisms.
example (f : R ‚Üí+* S) : PrimeSpectrum S ‚Üí PrimeSpectrum R :=
  f.specComap

-- This is the set `V(s) ‚à© D(f)`.
example (s : Set R) (f : R) : Set (PrimeSpectrum R) :=
  PrimeSpectrum.zeroLocus s ‚à© PrimeSpectrum.basicOpen f

/-!
### The unbundled vs. bundled barrier.

The composition of two ring homomorphisms can be expressed as:
-/
example (R S T : Type) [CommRing R] [CommRing S] [CommRing T] (f : R ‚Üí+* S) (g : S ‚Üí+* T) :
    R ‚Üí+* T :=
  RingHom.comp g f

/- or as: -/
example (R S T : CommRingCat) (f : R ‚ü∂ S) (g : S ‚ü∂ T) : R ‚ü∂ T :=
  f ‚â´ g

/-!
The first approach is called *unbundled* and the second one *bundled*: In the first version,
the `CommRing` structure on `R` is provided as a separate argument. It is unbundled from the
type `R`. In the second version, the `CommRing` structure is bundled with the type in a term
`R : CommRingCat`.

Note that we have to write `R ‚Üí+* S` in the first case to talk about a *ring homomorphism* `f`. This
is because `R S : Type`. In the case of `R S : CommRingCat`, the types contain enough information
to *infer* that `R ‚ü∂ S` denotes a ring homomorphism.

Moreover, in the bundled version we can use the notation `f ‚â´ g` to denote composition of the
ring homomorphisms `f` and `g`.
-/

/-!
Most of the topology and commutative algebra library is written in the unbundled style. But
to talk about the *category* of commutative rings or the *category* of topological spaces, this
category needs a *type of objects*.
-/

-- The category of commutative rings.
example : Type 1 := CommRingCat

-- The category of topological spaces.
example : Type 1 := TopCat

-- The type of commutative rings is endowed with a category structure.
example : Category CommRingCat := inferInstance

-- This allows us to write `ùüô _` for the identity and `_ ‚â´ _` for composition:
example (R S : CommRingCat) (f : R ‚ü∂ S) : R ‚ü∂ S := ùüô R ‚â´ f ‚â´ ùüô S

-- We can still apply a morphism in `CommRingCat` to an element.
example (R S : CommRingCat) (f : R ‚ü∂ S) (x : R) : S := f x

-- A morphism in `CommRingCat` has an underlying ring homomorphism.
example (R S : CommRingCat) (f : R ‚ü∂ S) : R ‚Üí+* S := f.hom

/-! Note: This requires `open CategoryTheory`! -/

/- If `R` is a commutative ring, `Spec R` is the affine *scheme* whose underlying topological space
is `PrimeSpectrum R`. -/
example (R : CommRingCat) : Scheme := Spec R

end PrimeSpectrum

section Definition

/-! ## Definition of `Scheme` -/

/- Use `F12` to go to definition. -/
#check Scheme

/-!
As you would expect, a `Scheme` is defined as a locally ringed space that is locally isomorphic
to the spectrum of a ring.
-/

-- As before, we can compose morphisms of schemes in the same way as we can compose
-- morphisms of commutative rings:
example (X Y : Scheme) (f : X ‚ü∂ Y) : X ‚ü∂ Y := f ‚â´ ùüô Y

-- We can apply a morphism of schemes to an element.
example (X Y : Scheme) (f : X ‚ü∂ Y) (x : X) : Y := f x

/-
Sections of `ùí™ = ùí™_X` over an open can be written with the notation `Œì(X, U)`.
-/
example (X : Scheme) (U : X.Opens) : CommRingCat := Œì(X, U)

/- If `U` is contained in `V`, we get a restriction map `ùí™(V) ‚ü∂ ùí™(U)` -/
example (X : Scheme) (U V : X.Opens) (hUV : U ‚â§ V) : Œì(X, V) ‚ü∂ Œì(X, U) :=
  X.presheaf.map (homOfLE hUV).op

variable {X Y : Scheme}

/- Given a morphism `f` and an open of `U`, we obtain a morphism `ùí™_Y(U) ‚ü∂ ùí™_X(f‚Åª¬π(U))`. -/
example (f : X ‚ü∂ Y) (U : Y.Opens) : Œì(Y, U) ‚ü∂ Œì(X, f ‚Åª¬π·µÅ U) :=
  f.app U

/- A variant we often encounter is the composition `ùí™_Y(U) ‚ü∂ ùí™_X(f‚Åª¬π(U)) ‚ü∂ ùí™_X(V)` -/
example (f : X ‚ü∂ Y) (U : Y.Opens) (V : X.Opens) (h : V ‚â§ f ‚Åª¬π·µÅ U) : Œì(Y, U) ‚ü∂ Œì(X, V) :=
  f.appLE U V h

/- Restriction of a morphism of schemes along an open of the target. -/
noncomputable example (f : X ‚ü∂ Y) (U : Y.Opens) : (f ‚Åª¬π·µÅ U : Scheme) ‚ü∂ U :=
  f ‚à£_ U

/-!
One of the reasons we use the bundled approach for `Scheme`s, is the heavy reliance on category
theoretical constructions.
-/

/- The fibre product of schemes is simply the application of the general `pullback` to the
category of `Schemes`. -/
noncomputable example (X Y Z : Scheme) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : Scheme :=
  pullback f g

/-
Note: `f ‚à£_ U` is *not* the projection `X √ó[Y] U ‚ü∂ U`, but sometimes
using `X √ó[Y] U` is convenient.
-/

/-! ### Affine schemes -/

/-- `Spec R` is affine. -/
example (R : CommRingCat) : IsAffine (Spec R) :=
  inferInstance

/-- If `X` is an affine scheme, it is isomorphic to `Spec Œì(X, ‚ä§)`. -/
noncomputable example (X : Scheme) [IsAffine X] : X ‚âÖ Spec Œì(X, ‚ä§) :=
  X.isoSpec

/-- Some proofs of being affine can be found by instance synthesis. -/
example {X Y Z : Scheme} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [IsAffine X] [IsAffine Y] [IsAffine Z] :
    IsAffine (pullback f g) :=
  inferInstance

end Definition

section CategoriesAndFunctors

/-! ## Categories and functors

We have already seen examples of categories, but not yet examples of functors.
-/

/- The `Spec` functor `CommRingCat·µí·µñ ‚•§ Scheme`. -/
#check AlgebraicGeometry.Scheme.Spec

example (R : CommRingCat) : Scheme.Spec.obj (Opposite.op R) = Spec R := rfl

/- In this language, the `Œì`-`Spec`-adjunction is phrased as: -/
noncomputable
example : Scheme.Œì.rightOp ‚ä£ Scheme.Spec := ŒìSpec.adjunction

end CategoriesAndFunctors

noncomputable section

namespace Stalks

/-! ## Stalks, residue fields and fibres -/

/-
To get acquainted with the scheme API, let us consider an example: Let us define
the fibre of a morphism of schemes.
-/

variable {X Y : Scheme} (f : X ‚ü∂ Y)

-- The stalk `ùí™_Y,y` of `ùí™_Y` at the point `y`.
example (y : Y) : CommRingCat := Y.presheaf.stalk y

-- The stalk `ùí™_Y,y` is a local ring.
#synth ‚àÄ y, IsLocalRing (Y.presheaf.stalk y)

-- And we may consider its residue field.
example (y : Y) : Type := IsLocalRing.ResidueField (Y.presheaf.stalk y)

-- The morphism `Spec Œ∫(y) ‚ü∂ Y`.
example (y : Y) : Spec (Y.residueField y) ‚ü∂ Y :=
  Y.fromSpecResidueField y

/-- The fibre of `f` over `y` is, by definition, the fibre product
```
X √ó[Y] Spec Œ∫(y) ------> Spec Œ∫(y)
      |                       |
      |                       |
      v                       |
      X --------------------> Y
```
-/
def fiber (y : Y) : Scheme :=
  pullback f (Y.fromSpecResidueField y)

/-- The immersion `X √ó[Y] Spec Œ∫(y) ‚ü∂ X`. -/
def fiberŒπ (y : Y) : fiber f y ‚ü∂ X :=
  pullback.fst f (Y.fromSpecResidueField y)

/-- The projection `X √ó[Y] Spec Œ∫(y) ‚ü∂ Spec Œ∫(y)`. -/
def fiberToSpecResidueField (y : Y) : fiber f y ‚ü∂ Spec (Y.residueField y) :=
  pullback.snd f (Y.fromSpecResidueField y)

/-!
In `mathlib` these are called `Hom.fiber`, `Hom.fiberŒπ` and `Hom.fiberToSpecResidueField` and we can
for example write `f.fiber`.
-/

end Stalks

section Subschemes

/-! ## Subschemes -/

/-! ### Open subschemes -/

variable {U X Y : Scheme}

-- Given an open subset of `X`, we can naturally regard it as a scheme.
example (U : X.Opens) : Scheme := U
example (U : X.Opens) : (U : Scheme) ‚ü∂ X := U.Œπ

/-!
Instead of working with `U : X.Opens`, it is often convenient to allow arbitrary
open immersions instead.
-/
example (f : U ‚ü∂ X) [IsOpenImmersion f] : X.Opens := f.opensRange

/-- We rely on typeclass inference to automatically fill proofs using stability properties. -/
example {V : Scheme} (f : U ‚ü∂ V) (g : V ‚ü∂ X) [IsOpenImmersion f] [IsOpenImmersion g] :
    IsOpenImmersion (f ‚â´ g) :=
  inferInstance

/-! ### Closed subschemes -/

example (f : Y ‚ü∂ X) [IsClosedImmersion f] : IsClosed (Set.range f) :=
  f.isClosedEmbedding.isClosed_range

/- A closed immersion determines an ideal sheaf. -/
example (f : Y ‚ü∂ X) [IsClosedImmersion f] : X.IdealSheafData := f.ker

/- And conversely, every ideal sheaf determines a closed immersion. -/
example : (MorphismProperty.Over @IsClosedImmersion ‚ä§ X)·µí·µñ ‚âå X.IdealSheafData :=
  IsClosedImmersion.overEquivIdealSheafData X

end Subschemes

section Properties

/-!
## Properties of morphisms

Mathlib knows many properties of morphisms. Browsing the `AlgebraicGeometry/Morphisms` folder
gives an overview. The properties are defined as type classes.
-/

example {X Y Z : Scheme} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [IsProper f] :
    IsSeparated (pullback.snd f g) :=
  inferInstance

/-- Properties of morphisms obtained from typeclass instances, usually have handy shortcuts. -/
example {X Y : Scheme} (f : X ‚ü∂ Y) [UniversallyInjective f] : Function.Injective f :=
  f.injective

/-! ### Morphism properties -/

/- A `MorphismProperty` is a property of morphisms. -/
variable (P : MorphismProperty Scheme)

/- There exist meta properties for morphism properties, for example
being stable under composition, base change, etc. -/
#check MorphismProperty.IsStableUnderComposition
#check MorphismProperty.IsStableUnderBaseChange
#check MorphismProperty.RespectsIso

/- But also some more technical ones: -/
#check MorphismProperty.HasOfPostcompProperty

example : MorphismProperty.HasOfPostcompProperty
    @IsEtale (@LocallyOfFiniteType ‚äì @FormallyUnramified) :=
  inferInstance

/- There are analogues for these in the language of commutative rings: -/
#check RingHom.StableUnderComposition
#check RingHom.IsStableUnderBaseChange
#check RingHom.RespectsIso

/-
Besides properties of properties, we also use abstract constructions of properties.
-/
#check MorphismProperty.universally
#check MorphismProperty.diagonal
#check topologically

end Properties

section ReductionToAffine

/-! ## Reduction to affine case -/

/-!
### (Open) covers

Any reduction to a local problem starts with an (affine) open cover. These
can be pulled back along morphisms, refined, etc.
-/

variable (X : Scheme)

#check Scheme.OpenCover

/-- Pullback an open cover along an arbitrary morphism. -/
example {X Y : Scheme} (f : X ‚ü∂ Y) (ùí∞ : Y.OpenCover) : X.OpenCover :=
  ùí∞.pullback‚ÇÅ f

/-- Refine every component of an open cover by an open cover. -/
example {X : Scheme} (ùí∞ : X.OpenCover) (ùí± : ‚àÄ i, (ùí∞.X i).OpenCover) : X.OpenCover :=
  ùí∞.bind ùí±

/-- A choice of affine cover of `X`. -/
example (X : Scheme) : X.OpenCover :=
  X.affineCover

/-- The components of `X.affineCover` are *definitionally equal* to some `Spec R` for
`R : CommRingCat`. -/
example (X : Scheme) (i : X.affineCover.I‚ÇÄ) :
    ‚àÉ R, X.affineCover.X i = Spec R :=
  ‚ü®_, rfl‚ü©

/-! ### Properties of properties -/

variable (P : MorphismProperty Scheme)

#check IsZariskiLocalAtTarget
#check IsZariskiLocalAtSource

#check IsZariskiLocalAtTarget.iff_of_openCover
#check IsZariskiLocalAtSource.iff_of_openCover

section

variable {X Y : Scheme.{u}}

/-! ### Example: Flat morphisms -/

@[mk_iff]
class Flat (f : X ‚ü∂ Y) : Prop where
  flat_of_isAffineOpen :
    ‚àÄ (U : Y.Opens) (V : X.Opens) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      IsAffineOpen U ‚Üí IsAffineOpen V ‚Üí (f.appLE U V e).hom.Flat

instance : HasRingHomProperty @Flat RingHom.Flat where
  isLocal_ringHomProperty := RingHom.Flat.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    rw [flat_iff, affineLocally_iff_affineOpens_le]
    simp only [Scheme.affineOpens, Set.coe_setOf, Set.mem_setOf_eq, Subtype.forall]
    grind

example : IsZariskiLocalAtTarget @Flat :=
  inferInstance

-- this should be in mathlib
/-- If `P = X √ó[Z] Y` and `Y ‚ü∂ Z` is an open immersion, then the stalk map
of `P ‚ü∂ Y` at some `x : P` is isomorphic to the stalk map of `X ‚ü∂ Z` at the image of `x`. -/
def stalkMapIsoOfIsPullback {X Y Z P : Scheme.{u}} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y}
    {f : X ‚ü∂ Z} (g : Y ‚ü∂ Z) (h : IsPullback fst snd f g) [IsOpenImmersion g] (x : P) :
    Arrow.mk (snd.stalkMap x) ‚âÖ Arrow.mk (f.stalkMap <| fst x) :=
  haveI : IsOpenImmersion fst := MorphismProperty.of_isPullback h.flip ‚Äπ_‚Ä∫
  Iso.symm <| Arrow.isoMk' _ _
    ((TopCat.Presheaf.stalkCongr _ <| .of_eq (congr($(h.1.1).base x))) ‚â™‚â´
      (asIso (g.stalkMap <| (snd x))))
    (asIso (fst.stalkMap <| x)) <| TopCat.Presheaf.stalk_hom_ext _ fun V hxV ‚Ü¶ by
      simp only [Scheme.Hom.comp_base, TopCat.hom_comp, ContinuousMap.comp_apply, Iso.trans_hom,
        TopCat.Presheaf.stalkCongr_hom, asIso_hom, Category.assoc,
        TopCat.Presheaf.germ_stalkSpecializes_assoc, Scheme.Hom.germ_stalkMap_assoc,
        Scheme.Hom.germ_stalkMap]
      simp only [‚Üê Scheme.Hom.comp_app_assoc, ‚Üê Scheme.Hom.comp_preimage]
      rw! [h.1.1]

theorem flat_of_flat_stalkMap (f : X ‚ü∂ Y) (H : ‚àÄ x, (f.stalkMap x).hom.Flat) :
    Flat f := by
  wlog hY : ‚àÉ R, Y = Spec R generalizing X Y f
  ¬∑ rw [IsZariskiLocalAtTarget.iff_of_openCover (P := @Flat) Y.affineCover]
    intro i
    refine this _ ?_ ‚ü®_, rfl‚ü©
    intro x
    rw [RingHom.Flat.respectsIso.arrow_mk_iso_iff]
    ¬∑ apply H
      dsimp at x
      exact pullback.fst f _ x
    ¬∑ dsimp [Scheme.Cover.pullbackHom]
      apply stalkMapIsoOfIsPullback (Y.affineCover.f i)
      apply IsPullback.of_hasPullback
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S generalizing X f
  ¬∑ rw [IsZariskiLocalAtSource.iff_of_openCover (P := @Flat) X.affineCover]
    intro i
    refine this _ ?_ ‚ü®_, rfl‚ü©
    intro x
    rw [Scheme.Hom.stalkMap_comp, CommRingCat.hom_comp,
      RingHom.Flat.respectsIso.cancel_right_isIso]
    apply H
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  rw [HasRingHomProperty.Spec_iff (P := @Flat)]
  apply RingHom.Flat.ofLocalizationPrime
  intro P hP
  specialize H ‚ü®P, hP‚ü©
  rwa [RingHom.Flat.respectsIso.arrow_mk_iso_iff (Scheme.arrowStalkMapSpecIso œÜ _)] at H

end

end ReductionToAffine

/-!
## Schemes over a base

We have multiple ways of talking about a scheme over a base.
-/

/- Version 1: A scheme over `Y` is simply a morphism `X ‚ü∂ Y`. -/
variable {X S : Scheme} (f : X ‚ü∂ S)

/- Version 2: There exists a typeclass `Scheme.Over` that bundles a preferred choice of morphism. -/
variable {X S : Scheme} [X.Over S]
example : X ‚ü∂ S := X ‚Üò S

/- Version 3: As a last resort, we can also use the over category. -/
variable {S : Scheme} (X : Over S)
example : X.left ‚ü∂ S := X.hom

/- In particular, we can take the base change. -/

/- In version 1: -/
variable {X S : Scheme} (f : X ‚ü∂ S)
example {T : Scheme} (g : T ‚ü∂ S) : pullback f g ‚ü∂ T := pullback.snd _ _

/- In version 3 -/
variable {S : Scheme} (X : Over S)
example {T : Scheme} (g : T ‚ü∂ S) : ((Over.pullback g).obj X).left ‚ü∂ T :=
  ((Over.pullback g).obj X).hom

/- In particular, we can do the above in the special case where `S = Spec k` for some field `k`. -/

/-! ## Varieties

There is no `AlgebraicGeometry.Variety` and there will most likely never be such a definition.
-/

/-- But you are free to create your local definition of variety (downstream of mathlib). -/
class Variety {X : Scheme} {k : Type} [Field k] (s : X ‚ü∂ Spec (.of k)) : Prop
    extends IsSeparated s, LocallyOfFiniteType s


/-! ## More topics -/

/- Function field of a scheme. -/
#check Scheme.functionField

/- (Locally) Noetherian schemes. -/
#check IsLocallyNoetherian
#check IsNoetherian

/- Projective spectrum of a graded ring. -/
variable {œÉ : Type} {A : Type}
variable [CommRing A] [SetLike œÉ A] [AddSubgroupClass œÉ A]
variable (ùíú : ‚Ñï ‚Üí œÉ) [GradedRing ùíú]
#check Proj ùíú
